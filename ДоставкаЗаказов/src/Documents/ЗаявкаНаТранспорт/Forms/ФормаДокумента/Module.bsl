
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТранспортНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка 	= Ложь;
	ТранспортСБригадой 		= ТранспортСБригадой(Объект.Дата);
	ОбщегоНазначенияКлиент.ФормированиеИОткрытиеФормы(ТранспортСБригадой, "Справочник.ТранспортныеСредства.ФормаВыбора", Элементы.Транспорт, "Наименование")
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, 
									ВыборДобавлением, СтандартнаяОбработка)
    СтандартнаяОбработка 	= Ложь;
    Объект.Транспорт 	= ВыбранноеЗначение;
	БригадаЗакрепленнаяЗаТранспортом();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ТранспортСБригадой(Дата)
	
	ТранспортСБригадой = Новый СписокЗначений();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаявкаНаТранспорт.Транспорт
		|ПОМЕСТИТЬ ВТЗанятыйТранспорт
		|ИЗ
		|	Документ.ЗаявкаНаТранспорт КАК ЗаявкаНаТранспорт
		|ГДЕ
		|	ЗаявкаНаТранспорт.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И ЗаявкаНаТранспорт.СостояниеЗаказа <> &СостояниеЗаказа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоставБригадыСрезПоследних.Транспорт
		|ИЗ
		|	РегистрСведений.СоставБригады.СрезПоследних(, Период МЕЖДУ &ДатаНачала И &ДатаОкончания) КАК СоставБригадыСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗанятыйТранспорт КАК ВТЗанятыйТранспорт
		|		ПО СоставБригадыСрезПоследних.Транспорт = ВТЗанятыйТранспорт.Транспорт.Ссылка
		|ГДЕ
		|	ВТЗанятыйТранспорт.Транспорт ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("СостояниеЗаказа", Перечисления.СостояниеЗаказа.Отработан);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(Дата));
	РезультатЗапроса = Запрос.Выполнить();
	
	Текст = "Состав бригады не назначен!";
	Выборка = ОбщегоНазначенияВызовСервера.ПроверкаРезультатаЗапроса(РезультатЗапроса, Текст);
	ТранспортСБригадой = ОбщегоНазначенияВызовСервера.ФормированиеСтруктурыОтбора(Выборка, Выборка.Транспорт);
	
	Возврат ТранспортСБригадой;

КонецФункции

&НаСервере
Процедура БригадаЗакрепленнаяЗаТранспортом()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоставБригадыСрезПоследних.Водитель.Ссылка КАК Водитель,
		|	СоставБригадыСрезПоследних.ГрузчикПервый.Ссылка КАК ГрузчикПервый,
		|	СоставБригадыСрезПоследних.ГрузчикВторой.Ссылка КАК ГрузчикВторой,
		|	СоставБригадыСрезПоследних.НачальныйПробег
		|ИЗ
		|	РегистрСведений.СоставБригады.СрезПоследних(, Период МЕЖДУ &ДатаНачала И &Дата) КАК СоставБригадыСрезПоследних
		|ГДЕ
		|	СоставБригадыСрезПоследних.Транспорт = &Транспорт";
	
	Запрос.УстановитьПараметр("Транспорт", Объект.Транспорт);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(Объект.Дата));
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	РезультатЗапроса = Запрос.Выполнить();
	
	Текст = "Состав бригады не назначен!";
	Выборка = ОбщегоНазначенияВызовСервера.ПроверкаРезультатаЗапроса(РезультатЗапроса, Текст);
	
	Выборка.Следующий();
	Объект.Водитель = Выборка.Водитель;
	Объект.ГрузчикПервый = Выборка.ГрузчикПервый;
	Объект.ГрузчикВторой = Выборка.ГрузчикВторой;
	Объект.НачальныйПробег = Выборка.НачальныйПробег;

КонецПроцедуры

#КонецОбласти
