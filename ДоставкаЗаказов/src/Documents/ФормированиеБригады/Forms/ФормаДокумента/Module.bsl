
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВодительНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка 	= Ложь;
	СвободныеВодители 		= СвободныеВодители(Объект.Дата);
	ТранспортныйОтделКлиент.ОткрытьФормуВыбора(СвободныеВодители, 
												"Справочник.Водители.ФормаВыбора", 
												Элементы.Водитель, 
												"Наименование");			
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка 	= Ложь;
	СвободныйТранспорт 		= СвободныйТранспорт(Объект.Дата);
	ТранспортныйОтделКлиент.ОткрытьФормуВыбора(СвободныйТранспорт, 
												"Справочник.ТранспортныеСредства.ФормаВыбора", 
												Элементы.Транспорт, 
												"РегистрационныйНомер");	
    
КонецПроцедуры

&НаКлиенте
Процедура ГрузчикПервыйНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка 	= Ложь;
	СвободныеГрузчики 		= ДобавитьГрузчиковИзДокумента();
	ТранспортныйОтделКлиент.ОткрытьФормуВыбора(СвободныеГрузчики, 
												"Справочник.Грузчики.Форма.ФормаВыбора", 
												Элементы.ГрузчикПервый, 
												"Наименование");
    
КонецПроцедуры

&НаКлиенте
Процедура ГрузчикВторойНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка 	= Ложь;
	СвободныеГрузчики 		= ДобавитьГрузчиковИзДокумента();
	ТранспортныйОтделКлиент.ОткрытьФормуВыбора(СвободныеГрузчики, 
												"Справочник.Грузчики.Форма.ФормаВыбора", 
												Элементы.ГрузчикВторой, 
												"Наименование");
    
КонецПроцедуры

&НаКлиенте
Процедура ВодительОбработкаВыбора(Элемент, 
								ВыбранноеЗначение, 
								ДополнительныеДанные, 
								ВыборДобавлением, 
								СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка 	= Ложь;
    Объект.Водитель 		= ВыбранноеЗначение;
    
КонецПроцедуры

&НаКлиенте
Процедура ТранспортОбработкаВыбора(Элемент, 
								ВыбранноеЗначение, 
								ДополнительныеДанные, 
								ВыборДобавлением, 
								СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка 	= Ложь;
	Объект.Транспорт 		= ВыбранноеЗначение;
	Объект.НачальныйПробег 	= ПробегТранспорта(Объект.Транспорт);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузчикПервыйОбработкаВыбора(Элемент, 
								ВыбранноеЗначение, 
								ДополнительныеДанные, 
								ВыборДобавлением, 
								СтандартнаяОбработка) Экспорт
   
    СтандартнаяОбработка 	= Ложь;
    Объект.ГрузчикПервый 	= ВыбранноеЗначение;
    
КонецПроцедуры

&НаКлиенте
Процедура ГрузчикВторойОбработкаВыбора(Элемент, 
								ВыбранноеЗначение, 
								ДополнительныеДанные, 
								ВыборДобавлением, 
								СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка 	= Ложь;
    Объект.ГрузчикВторой 	= ВыбранноеЗначение;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СвободныеВодители(Дата)
	
	СвободныеВодители = Новый СписокЗначений;
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Водители.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Водители КАК Водители
	|ГДЕ
	|	Водители.Наименование НЕ В
	|		(ВЫБРАТЬ
	|			СоставБригады.Водитель.Наименование
	|		ИЗ
	|			РегистрСведений.СоставБригады КАК СоставБригады
	|		ГДЕ
	|			СоставБригады.Период = &Период)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Запрос.УстановитьПараметр("Период", НачалоДня(Дата));
	РезультатЗапроса 	= Запрос.Выполнить();
	Текст = "Все водители распределены по бригадам";
	Выборка = ТранспортныйОтделВызовСервера.ВыборкаРезультатаЗапроса(РезультатЗапроса, Текст);
	СвободныеВодители = ТранспортныйОтделВызовСервера.НоваяСтруктураОтбора(Выборка, Выборка.Наименование);
		
	Возврат СвободныеВодители;	
		
КонецФункции

&НаСервереБезКонтекста
Функция СвободныйТранспорт(Дата)
	
	СвободныйТранспорт = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТранспортныеСредства.Наименование КАК Наименование,
	|	ТранспортныеСредства.РегистрационныйНомер
	|ИЗ
	|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
	|ГДЕ
	|	ТранспортныеСредства.Наименование НЕ В
	|		(ВЫБРАТЬ
	|			СоставБригады.Транспорт.Наименование
	|		ИЗ
	|			РегистрСведений.СоставБригады КАК СоставБригады
	|		ГДЕ
	|			СоставБригады.Период = &Период)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Запрос.УстановитьПараметр("Период", НачалоДня(Дата));
	РезультатЗапроса 	= Запрос.Выполнить();
	Текст 				= "Весь транспорт распределен по бригадам";
	Выборка 			= ТранспортныйОтделВызовСервера.ВыборкаРезультатаЗапроса(РезультатЗапроса, Текст);
	СвободныйТранспорт 	= ТранспортныйОтделВызовСервера.НоваяСтруктураОтбора(Выборка, Выборка.РегистрационныйНомер);
	
	Возврат СвободныйТранспорт;	

КонецФункции

&НаСервереБезКонтекста
Функция СвободныеГрузчики(Дата, Ссылка)
	
	СвободныеГрузчики = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Грузчики.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Грузчики КАК Грузчики
	|ГДЕ
	|	Грузчики.Наименование НЕ В
	|		(ВЫБРАТЬ
	|			СоставБригады.ГрузчикПервый.Наименование
	|		ИЗ
	|			РегистрСведений.СоставБригады КАК СоставБригады
	|		ГДЕ
	|			СоставБригады.Период = &Период)
	|	И Грузчики.Наименование НЕ В
	|		(ВЫБРАТЬ
	|			СоставБригады.ГрузчикВторой.Наименование
	|		ИЗ
	|			РегистрСведений.СоставБригады КАК СоставБригады
	|		ГДЕ
	|			СоставБригады.Период = &Период)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Запрос.УстановитьПараметр("Период", НачалоДня(Дата));
	РезультатЗапроса 	= Запрос.Выполнить();
	Текст = "Все грузчики распределены по бригадам";
	Выборка = ТранспортныйОтделВызовСервера.ВыборкаРезультатаЗапроса(РезультатЗапроса, Текст);
	СвободныеГрузчики = ТранспортныйОтделВызовСервера.НоваяСтруктураОтбора(Выборка, Выборка.Наименование);

	Возврат СвободныеГрузчики;	
	
КонецФункции

&НаКлиенте
Функция ДобавитьГрузчиковИзДокумента()
	
	СвободныеГрузчики = СвободныеГрузчики(Объект.Дата, Объект.Ссылка);
	СвободныеГрузчики = ГрузчикВДокументе(СвободныеГрузчики, Объект.ГрузчикПервый);
	СвободныеГрузчики = ГрузчикВДокументе(СвободныеГрузчики, Объект.ГрузчикВторой);	
	
	Если СвободныеГрузчики.Количество() = 0 Тогда
		Текст = "Все грузчики распределены по бригадам";
		ОбщегоНазначенияКлиент.СообщитьПользователюКлиент(Текст);
	КонецЕсли;
	
	Возврат	СвободныеГрузчики;
		
КонецФункции

&НаКлиенте
Функция ГрузчикВДокументе(НаименованиеСтруктуры, ОбъектНаименование)
	
	ВведенныйРанееГрузчик = НаименованиеСтруктуры.НайтиПоЗначению(Строка(ОбъектНаименование));
	
	Если ЗначениеЗаполнено(ОбъектНаименование) И ВведенныйРанееГрузчик <> Неопределено Тогда
		НаименованиеСтруктуры.Удалить(ВведенныйРанееГрузчик);
	КонецЕсли;
		
	Возврат	НаименованиеСтруктуры;
		
КонецФункции

&НаСервереБезКонтекста
Функция ПробегТранспорта(Транспорт)

	НачальныйПробег = РегистрыНакопления.ПробегТранспорта.ПробегТранспорта(Транспорт);
	Возврат НачальныйПробег; 
	
КонецФункции

#КонецОбласти

